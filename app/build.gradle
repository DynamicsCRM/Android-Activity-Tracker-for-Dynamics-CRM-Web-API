apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    lintOptions {
        abortOnError false
    }

    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "0.0.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
//        Retrolambda is brought in so I can utilize Java 8 Lambda functionality, since
//        Official support with the jack compiler isn't available with use of Annotation
//        Processors
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

repositories {
    mavenCentral()

    flatDir {
        dirs 'libs'
    }

    maven { url 'https://jitpack.io' }
}

def dbflow_version = "3.0.1"

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':xrm')

//    Debug tools for finding memory leaks, checking network calls, debug views, and
//    check the database with sql queries and table checks
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-urlconnection:1.3.1'

//    Google libraries
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'

//    Google's Dagger Dependency Injection Annotation Processor
    apt 'com.google.dagger:dagger-compiler:2.2'
    compile 'com.google.dagger:dagger:2.2'

//    Google's Databinding Annotation Processor
    apt 'com.android.databinding:compiler:2.0.0-rc3'

//    Database ORM Annotation Processor
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"

//    RXJava is the famous reactive library used in all kinds of libraries, this is very powerful
//    and allows you to no longer have to use huge messy AsyncTasks. RXAndroid allows you to go right
//    back to the UI Thread with a AndroidScheduler.
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.2'

//    Square's Networking framework build on top of their okhttp library, along with the
//    jackson converter and adapter for rxjava so responses can be observable
    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.1'

//    Common library to reduce boilerplate code when working with views. Allowing you to
//    "bind" events with methods and auto bind views to variables.
    compile 'com.jakewharton:butterknife:7.0.1'


    compile 'org.mod4j.org.apache.commons:lang:2.1.0'
    compile('com.microsoft.aad:adal:1.2.0') {
        exclude group: 'com.android.support'
    }

}